load events-query

mod L-QUERY is inc EVENTS-QUERY .
  inc OBJS .
  inc SAMPLING-LIB .
  
  vars O O' O'' : Address .  vars QS QS' : Queries .
  var OS : Oids .  var R : Map{Key,Oids} .
  vars AS AS' : AttributeSet .  var K : Key .
  var DB : Map{Key,Data} .  vars V V' : Value .
  var P : Content .  vars DAT DAT' : Data .  
  vars TS TS' : Timestamp .  var ID : Id .
  var RS : Map{Id,Data} .  
  var Q : Query .
  vars CLS SS : Map{Id,Oids} .
  var LOAD : Map{Address,Float} .
  vars COUNT RAND F1 F2 F3 : Float .
  var GT : Float .
  var AC : ActorConfig .
    
***monitor
  rl [delegate] : 
     < log : Monitor | events: TES:TimedEvents >
     {GT, to O : startQuery}
     < O : Client | queries: (query(ID,K) :: QS), servers: LOAD, AS >
   =>
     < log : Monitor | events: (TES:TimedEvents ; (proxy(localBalancedServer(LOAD)) @ GT)) >
     < O : Client | queries: QS, servers: incr(localBalancedServer(LOAD),LOAD), AS >  
	 [md, to localBalancedServer(LOAD) from O : query(ID,K)] .
	 
  op incr : Address Map{Address,Float} -> Map{Address,Float} .
  eq incr(O,(LOAD, O |-> COUNT)) = LOAD, O |-> (COUNT + 1.0) .
  
  op localBalancedServer : Map{Address,Float} -> Address .
  op localBalancedServer : Map{Address,Float} Float Float -> Address .
  eq localBalancedServer(LOAD) = localBalancedServer(LOAD,rand,totalLoads(LOAD)) . 
  eq localBalancedServer((O |-> F1, O' |-> F2, O'' |-> F3),RAND,COUNT)
   = if RAND < F1 / COUNT
       then O
	   else if RAND < F1 / COUNT + F2 / COUNT
	          then O'
			  else O''
			fi
	 fi .
  
  op totalLoads : Map{Address,Float} -> Float .
  eq totalLoads((LOAD, O |-> COUNT)) = totalLoads(LOAD) + COUNT .
  eq totalLoads(empty) = 0.0 .
  	 

  rl [propagate] :
     {GT, to O from O' : query(ID,K)}
     < O : Server | executing: QS, waiting: SS,
	                replicas: R, clients: CLS, AS >   
   =>
     < O : Server | executing: (QS :: query(ID,K)), waiting: insert(ID,R[K],SS), 
	                replicas: R, clients: insert(ID,O',CLS), AS >
     (propagate read(ID,K) to R[K] from O) .  
 
 
  op propagate_to_from_ : Query Oids Address -> Config .
  eq propagate Q to (O ; OS) from O' = (propagate Q to OS from O') 
     [md, (to O from O' : Q)] .
  eq propagate Q to empty from O' = null . 
  

***monitor
  rl [reply] :
     < log : Monitor | events: TES:TimedEvents >
     {GT, to O from O' : read(ID,K)}
     < O : Server | database: DB, AS >
  => 
     < log : Monitor | events: (TES:TimedEvents ; (replicaValue(ID,DB[K]) @ GT)) >
     < O : Server | database: DB, AS > 
     [md, to O' from O : reply(ID,DB[K])] .  
 

  rl [update] :
     {GT, to O from O' : reply(ID,DAT')}
     < O : Server | waiting: (SS, ID |-> (O' ; OS)), results: RS, AS >
  => 
     < O : Server | waiting: (SS, ID |-> OS), results: latest(ID,DAT',RS), AS > .
	 

  op latest : Id Data Map{Id,Data} -> Map{Id,Data} .
  eq latest(ID,DAT',(RS, ID |-> DAT)) 
   = RS, ID |-> fresher(DAT,DAT') .
  eq latest(ID,DAT',RS) = insert(ID,DAT',RS) [owise] .
  
  op fresher : Data Data -> Data .
  eq fresher(< V,TS >,null) = < V,TS > .
  eq fresher(null,< V,TS >) = < V,TS > .
  eq fresher(< V,TS >,< V',TS' >) = if TS >= TS' then < V,TS > else < V',TS' > fi .   
	 

***monitor
  rl [ack] :
     { GT:Float | SL:ScheduleList }
     < log : Monitor | events: TES:TimedEvents >
     < O : Server | executing: (QS :: query(ID,K) :: QS'), waiting: (SS, ID |-> empty), 
	                clients: (CLS, ID |-> O'), results: (RS, ID |-> DAT), AS >
  => 
     { GT:Float | SL:ScheduleList }
     < log : Monitor | events: (TES:TimedEvents ; (returnedValue(ID,DAT) @ GT)) >
     < O : Server | executing: (QS :: QS'), waiting: SS,
	                clients: CLS, results: RS, AS > 
     [md, to O' : startQuery] .

  *** do NOT forget AC
  eq eagerEnabled(AC < O : Server | executing: (QS :: query(ID,K) :: QS'), waiting: (SS, ID |-> empty), 
	                                clients: (CLS, ID |-> O'), results: (RS, ID |-> DAT), AS >) 
   = true .


  rl [finish] :
     {GT, to O : startQuery}
     < O : Client | queries: ept, AS >
   =>
     < O : Client | queries: ept, AS > .
 
endm
