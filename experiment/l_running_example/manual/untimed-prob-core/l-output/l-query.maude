load ../apmaude
load ../sampling-lib
***
load events-query

mod SORTS is
  inc SCHEDULING .
  pr NAT .
  
  sorts Query Queries Key Value Timestamp Data Oids Id .
  subsort Data < Payload .
  subsort Nat < Timestamp .
  subsort Nat < Value .
  subsort Query < Queries < Payload .
  subsort Oid < Oids .
  subsort Nat < Id .

  op <_`,_> : Value Timestamp -> Data [ctor] .
  op null : -> Data .
  
  op query : Id Key -> Query .
  op read : Id Key -> Query .
  
  op ept : -> Queries .
  op _::_ : Queries Queries -> Queries [assoc id: ept] .

  op empty : -> Oids .
  op _;_ : Oids Oids -> Oids [assoc comm id: empty] .  
  
  op reply : Id Data -> Payload .
endm
view Key from TRIV to SORTS is sort Elt to Key . endv
view Data from TRIV to SORTS is sort Elt to Data . endv
view Oids from TRIV to SORTS is sort Elt to Oids . endv
view Id from TRIV to SORTS is sort Elt to Id . endv

mod OBJS is
  inc SORTS .
  pr MAP{Key,Data} .
  pr MAP{Key,Oids} .
  pr MAP{Id,Data} .
  pr MAP{Id,Oids} .
***
  pr MAP{Oid,Float} .
  
  ops Client Server : -> ObjType .
  
  op queries:_ : Queries -> Attribute .
  op waiting:_ : Map{Id,Oids}  -> Attribute .
  op replicas:_ : Map{Key,Oids} -> Attribute .  
  op results:_ : Map{Id,Data} -> Attribute .
  op database:_ : Map{Key,Data} -> Attribute .
  
***  
  op servers:_ : Map{Oid,Float} -> Attribute .
  
  op clients:_ : Map{Id,Oids} -> Attribute .
   
  op executing:_ : Queries -> Attribute . 
  
  op startQuery : -> Payload .
endm


mod L-QUERY is
  inc OBJS .
  inc SAMPLE-UNIFORM .
***
  inc EVENTS-QUERY .
  
  vars O O' O'' TB : Oid .  vars QS QS' : Queries .
  var OS : Oids .  var R : Map{Key,Oids} .
  vars AS AS' : Attributes .  var K : Key .
  var DB : Map{Key,Data} .  vars V V' : Value .
  var P : Payload .  vars DAT DAT' : Data .  
  vars TS TS' : Timestamp .  var ID : Id .
  var RS : Map{Id,Data} .  
  var Q : Query .
  vars CLS SS : Map{Id,Oids} .
  var LOAD : Map{Oid,Float} .
  vars COUNT RAND F1 F2 F3 : Float .
  
 
*** Monitor  
  rl [delegate] : 
     < log : Monitor | events: ES:Events >  
     (to O : startQuery)
     < O : Client | queries: (query(ID,K) :: QS), servers: LOAD, AS >
   =>
     < log : Monitor | events: (ES:Events ; proxy(localBalancedServer(LOAD))) >
     < O : Client | queries: QS, servers: incr(localBalancedServer(LOAD),LOAD), AS >  
	 (to localBalancedServer(LOAD) from O : query(ID,K)) .
	 
  op incr : Oid Map{Oid,Float} -> Map{Oid,Float} .
  eq incr(O,(LOAD, O |-> COUNT)) = LOAD, O |-> (COUNT + 1.0) .
  
  op localBalancedServer : Map{Oid,Float} -> Oid .
  op localBalancedServer : Map{Oid,Float} Float Float -> Oid .
  eq localBalancedServer(LOAD) = localBalancedServer(LOAD,rand,totalLoads(LOAD)) . 
  eq localBalancedServer((O |-> F1, O' |-> F2, O'' |-> F3),RAND,COUNT)
   = if RAND < F1 / COUNT
       then O
	   else if RAND < F1 / COUNT + F2 / COUNT
	          then O'
			  else O''
			fi
	 fi .
  
  op totalLoads : Map{Oid,Float} -> Float .
  eq totalLoads((LOAD, O |-> COUNT)) = totalLoads(LOAD) + COUNT .
  eq totalLoads(empty) = 0.0 .
  	 

  rl [propagate] :
     (to O from O' : query(ID,K))
     < O : Server | executing: QS, waiting: SS,
	                replicas: R, clients: CLS, AS >   
   =>
     < O : Server | executing: (QS :: query(ID,K)), waiting: insert(ID,R[K],SS), 
	                replicas: R, clients: insert(ID,O',CLS), AS >
     (propagate read(ID,K) to R[K] from O) .  
 
 
  op propagate_to_from_ : Query Oids Oid -> Msgs .
  eq propagate Q to (O ; OS) from O' = (propagate Q to OS from O') (to O from O' : Q) .
  eq propagate Q to empty from O' = null . 
  

*** Monitor
  rl [reply] :
     < log : Monitor | events: ES:Events >
     (to O from O' : read(ID,K)) 
     < O : Server | database: DB, AS >
  => 
     < log : Monitor | events: (ES:Events ; replicaValue(ID,DB[K])) >
     < O : Server | database: DB, AS > 
     (to O' from O : reply(ID,DB[K])) .  
 

  rl [update] :
     (to O from O' : reply(ID,DAT')) 
     < O : Server | waiting: (SS, ID |-> (O' ; OS)), results: RS, AS >
  => 
     < O : Server | waiting: (SS, ID |-> OS), results: latest(ID,DAT',RS), AS > .
	 

  op latest : Id Data Map{Id,Data} -> Map{Id,Data} .
  eq latest(ID,DAT',(RS, ID |-> DAT)) 
   = RS, ID |-> fresher(DAT,DAT') .
  eq latest(ID,DAT',RS) = insert(ID,DAT',RS) [owise] .
  
  op fresher : Data Data -> Data .
  eq fresher(< V,TS >,null) = < V,TS > .
  eq fresher(null,< V,TS >) = < V,TS > .
  eq fresher(< V,TS >,< V',TS' >) = if TS >= TS' then < V,TS > else < V',TS' > fi .   
	 
   
*** Monitor   
  rl [ack] :
     < log : Monitor | events: ES:Events >
     < O : Server | executing: (QS :: query(ID,K) :: QS'), waiting: (SS, ID |-> empty), 
	                clients: (CLS, ID |-> O'), results: (RS, ID |-> DAT), AS >
  => 
     < log : Monitor | events: (ES:Events ; returnedValue(ID,DAT)) >
     < O : Server | executing: (QS :: QS'), waiting: SS,
	                clients: CLS, results: RS, AS > 
     (to O' : startQuery) .
	 
  rl [finish] :
     (to O : startQuery)
     < O : Client | queries: ept, AS >
   =>
     < O : Client | queries: ept, AS > .
	 
endm
