load ../apmaude

  *** P *** file name format: fixed
load ../sampling-lib



(mod SORTS is
  inc SCHEDULING .
  pr NAT .
  
  sorts Query Queries Key Value Timestamp Data Oids Id .
  subsort Data < Payload .
  subsort Nat < Timestamp .
  subsort Nat < Value .
  subsort Query < Queries < Payload .
  subsort Oid < Oids .
  subsort Nat < Id .

  op <_`,_> : Value Timestamp -> Data [ctor] .
  op null : -> Data .
  
  op read : Id Key -> Query .
  
  op ept : -> Queries .
  op _::_ : Queries Queries -> Queries [assoc id: ept] .

  op empty : -> Oids .
  op _;_ : Oids Oids -> Oids [assoc comm id: empty] .  
  
  op reply : Id Data -> Payload .

  op undefined : -> Qid .
endm)



(view Key from TRIV to SORTS is sort Elt to Key . endv)
(view Data from TRIV to SORTS is sort Elt to Data . endv)
(view Oids from TRIV to SORTS is sort Elt to Oids . endv)
(view Id from TRIV to SORTS is sort Elt to Id . endv)

(mod OBJS is
  inc SORTS .
  pr MAP{Key,Data} .
  pr MAP{Key,Oids} .
  pr MAP{Id,Data} .
  
  ops Client Server : -> ObjType .
  
  op queries:_ : Queries -> Attribute .
  op waiting:_ : Oids  -> Attribute .
  op replicas:_ : Map{Key,Oids} -> Attribute .  
  op results:_ : Map{Id,Data} -> Attribute .
  op database:_ : Map{Key,Data} -> Attribute .
endm)


(mod QUERY is
  inc OBJS .
  
  vars O O' TB : Oid .  vars QS QS' : Queries .
  var OS : Oids .  var R : Map{Key,Oids} .
  vars AS AS' : Attributes .  var K : Key .
  var DB : Map{Key,Data} .  vars V V' : Value .
  var P : Payload .  vars DAT DAT' : Data .  
  vars TS TS' : Timestamp .  var ID : Id .
  var RS : Map{Id,Data} .
  



  rl [update] :
     (to O from O' : reply(ID,DAT')) 
     < O : Client | waiting: (O' ; OS), results: (RS, ID |-> DAT), AS >
  => 
     < O : Client | waiting: OS, results: (RS, ID |-> latest(DAT,DAT')), AS > .

    
  op latest : Data Data -> Data .
  eq latest(< V,TS >,null) = < V,TS > .
  eq latest(null,< V,TS >) = < V,TS > .
  eq latest(< V,TS >,< V',TS' >) = if TS >= TS' then < V,TS > else < V',TS' > fi .


 crl [issue] :
     < O : Client | queries: (read(ID,K) :: QS), waiting: empty,
                   replicas: R, results: RS, AS > 
  => 
     < O : Client | queries: (read(ID,K) :: QS), waiting: (R[K]),
                   replicas: R, results: insert(ID,null,RS), AS > 
     (propagate read(ID,K) to R[K] from O)
    if not $hasMapping(RS,ID) .



  op propagate_to_from_ : Payload Oids Oid -> Msgs . 
  eq propagate P to (O ; OS) from O' = (propagate P to OS from O') (to O from O' : P) .
  eq propagate P to empty from O' = null .



  rl [req] :
     (to O : QS')
     < O : Client | queries: QS, AS > 
  =>
     < O : Client | queries: (QS :: QS'), AS > .
  



   
  rl [reply] :
     (to O from O' : read(ID,K)) 
     < O : Server | database: DB, AS >
  => 
     < O : Server | database: DB, AS > 
     (to O' from O : reply(ID,DB[K])) .


   


   
  rl [finish] :
     < O : Client | queries: (read(ID,K) :: QS), waiting: empty, results: (RS, ID |-> DAT), AS >
  => 
     < O : Client | queries: QS, waiting: empty, results: (RS, ID |-> DAT), AS > .
endm)

