load full-maude

fmod TAG-COMMANDS is
    including COMMANDS .

    op monitor_. : @ModExp@ -> @Command@ .
    op smonitor_. : @ModExp@ -> @Command@ .
endfm 


fmod META-TAG-SIGNATURE is
	including META-FULL-MAUDE-SIGN .
	
	op TagGRAMMAR : -> FModule .
	eq TagGRAMMAR = addImports((including 'TAG-COMMANDS .), GRAMMAR ) .

endfm

fmod TAG-BUILTINS is
    including UNIT .
    including DATABASE-DECLS  .
    including EXT-QID-LIST .
    
    op tag : OModule -> OModule .
    op tag : SModule -> SModule .
    op tagS : SModule SModule -> SModule .
    
    op tagID : Qid -> Qid .
    
    
    vars SM SM' : SModule .
    vars Q Q1 : Qid .
    vars IL IL1 : ImportList .
    vars S S1 : SortSet .
    vars SS SS1 : SubsortDeclSet .
    vars CDS CDS1 : ClassDeclSet .
    vars SCDS SCDS1 : SubclassDeclSet .
    vars O O1 : OpDeclSet .
    vars MS MS1 : MsgDeclSet .
    vars M M1 : MembAxSet .
    vars E E1 : EquationSet .
    vars R R1 : RuleSet .
    vars DB : Database .
    
    eq tag(omod Q is
	  IL
    sorts S .
    SS
    CDS
    SCDS
    O
    MS
    M
    E
    R
endom) = omod tagID(Q)  is
IL
    sorts S .
    SS
    CDS
    SCDS 
    O
    MS
    M
    E
    monitorRules(R)
endom .

    eq tag(mod Q is
	  IL
    sorts S .
    SS
	O
    M
    E
    R
endm) = mod tagID(Q)  is
IL
    sorts S .
    SS
    O 
	M
    E
    monitorRules(R)
endm .

ceq tagS(mod Q1 is IL1 sorts S1 . SS1 O1 M1 E1 R1 endm, 
	    mod Q is IL sorts S . SS O M E R endm) = 
   mod tagID(Q)  is IL sorts S . SS O M
    	changeMsgFuncs(SM',E)
		smonitorRules(SM',R) endm 
if SM' := mod Q1 is IL1 sorts S1 . SS1 O1 M1 none  none endm .


    vars T T' : Term .
    var ATS : AttrSet .
    var B : Bool .
    var CD : Condition .
    var ATR : AttrSet .
    var Rl : Rule .
    var MD : Module .
    var TY : Type .
    op monitorRules : RuleSet  -> RuleSet .
    op smonitorRules : SModule RuleSet  -> RuleSet .    
    op monitorRulesAux : Rule  -> RuleSet .
    op smonitorRulesAux : SModule Rule  -> RuleSet .

    
    ops newMonitorObj snewMonitorObj   : -> Term .
    ops newMonitorObjRecord snewMonitorObjRecord : Term -> Term .
    ops newMonitorObjRecord2 snewMonitorObjRecord2 : Term -> Term .    
    ops resultMonitorObj : Term Term -> Term .
    ops resultMonitorObjInsert sresultMonitorObjInsert : Term Term -> Term .
    ops resultMonitorObjInsert sresultMonitorObjInsert : Term Term Term Term Term -> Term .
    ops resultMonitorObjInsert sresultMonitorObjInsert : Term Term Term Term Term Term -> Term .
    ops updateMsgL updateMsgR : SModule Term -> Term .

    op changeMsgFuncs : Module EquationSet -> EquationSet .
    op changeMsgFuncsRHS : Module Term -> Term .


    eq changeMsgFuncs(MD, (eq T = T' [ATR] .) E) = 
       (eq T =  changeMsgFuncsRHS(MD, T') [ATR] .) changeMsgFuncs(MD, E) .  


    eq changeMsgFuncs(MD, (ceq T = T' if CD [ATR] .) E) = 
       (ceq T =  changeMsgFuncsRHS(MD, T') if CD [ATR] .) changeMsgFuncs(MD, E) .


    eq changeMsgFuncs(MD,none) = none .   

    ceq changeMsgFuncsRHS(MD, T) =  '`[_`,_`]['delay.Delay, T] if getType(metaReduce(MD, T)) == 'Msg .
    ceq changeMsgFuncsRHS(MD,Q[TL]) = Q[changeMsgFuncsRHS(MD,TL)] if getType(metaReduce(MD, Q[TL])) =/= 'Msg .
    ceq changeMsgFuncsRHS(MD,(T,TL)) = changeMsgFuncsRHS(MD,T), changeMsgFuncsRHS(MD,TL) if TL =/= empty .
    eq changeMsgFuncsRHS(MD,T) = T [owise] .


    *** here are the functions to generate terms for tagged monitors
    *** I try to make them as generic as possible, but some paras are fixed for the simplicity 
    eq newMonitorObj = '<_:_|_>['M@M:Address,'Monitor.ActorType,'_`,_['clock:_['GT@M:Nat],'log:_[
	      'LOG@M:Log]]] .

    ceq updateMsgL(MD, T) =  '`{_`,_`}['GT@M:Float,T] if getType(metaReduce(MD, T)) == 'Msg .
    ceq updateMsgL(MD,Q[TL]) = Q[updateMsgL(MD,TL)] if getType(metaReduce(MD, Q[TL])) =/= 'Msg .
    ceq updateMsgL(MD,(T,TL)) = updateMsgL(MD,T), updateMsgL(MD,TL) if TL =/= empty .
    eq updateMsgL(MD,T) = T [owise] .


    eq snewMonitorObj = '<_:_|_>['M@M:Address,'Monitor.ActorType,'log:_['LOG@M:Log]] .    


    eq newMonitorObjRecord(T1) =
	'<_:_|_>['M:Address,'Monitor.ActorType,'_`,_['clock:_['GT@M:Nat],'log:_[
	      '_`,_['_|->_[T1,'<_`,_`,_`,_`,_>['_|->_['RID@M:Address,'T1@M:Nat],'VTS2@M:VectorTime,
		    'FFLAG@M:Bool,'READS@M:Set`{KeyVersion`},'WRITES@M:Set`{KeyVersion`}]],'LOG@M:Log]]]] .

    eq snewMonitorObjRecord(T1) =
  '<_:_|_>['M:Address,'Monitor.ActorType,'log:_[
        '_`,_['_|->_[T1,'<_`,_`,_`,_`,_>['_|->_['RID@M:Address,'T1@M:Nat],'VTS2@M:VectorTime,
        'FFLAG@M:Bool,'READS@M:Set`{KeyVersion`},'WRITES@M:Set`{KeyVersion`}]],'LOG@M:Log]]] .

    eq newMonitorObjRecord2(T1) =
    '<_:_|_>['M@M:Address,'Monitor.ActorType,'_`,_['clock:_['GT@M:Nat],'log:_[
          '_`,_['_|->_[T1,'<_`,_`,_`,_`,_>['VTS1@M:VectorTime,'VTS2@M:VectorTime,
            'FFLAG@M:Bool,'READS@M:Set`{KeyVersion`},'WRITES@M:Set`{KeyVersion`}]],'LOG@M:Log]]]] .

    eq snewMonitorObjRecord2(T1) =
    '<_:_|_>['M@M:Address,'Monitor.ActorType,'log:_[
          '_`,_['_|->_[T1,'<_;_;_;_;_>['VTS1@M:VectorTime,'VTS2@M:VectorTime,
            'FFLAG@M:Bool,'READS@M:Set`{KeyVersion`},'WRITES@M:Set`{KeyVersion`}]],'LOG@M:Log]]] .
                    

    eq resultMonitorObjInsert(T1,T2) =
    '<_:_|_>['M@M:Address,'Monitor.ActorType,'_`,_['clock:_['_+_['GT@M:Nat,'s_[
      '0.Zero]]],'log:_['_`,_['_|->_[T1,'<_`,_`,_`,_`,_>['VTS1@M:VectorTime,
        'insert[T2,'GT@M:Nat,'VTS2@M:VectorTime],'FFLAG@M:Bool,'READS@M:Set`{KeyVersion`},'WRITES@M:Set`{KeyVersion`}]],'LOG@M:Log]]]] .

    eq sresultMonitorObjInsert(T1,T2) =
      '<_:_|_>['M@M:Address,'Monitor.ActorType,'log:_['insert[T1,
        '<_;_;_;_;_>['_|->_[T2,'GT@M:Float],'empty.VectorTime,'false.Bool,
        'empty.Set`{KeyVersion`},'empty.Set`{KeyVersion`}],'LOG@M:Log]]] .


    eq resultMonitorObjInsert(T1,T2,T3,T4,T5) =
	'<_:_|_>['M@M:Address,'Monitor.ActorType,'_`,_['clock:_['_+_['GT@M:Nat,'s_[
		  '0.Zero]]],'log:_['_`,_['_|->_[T1,'<_`,_`,_`,_`,_>['VTS1@M:VectorTime,
		    'insert[T2,'GT@M:Nat,'VTS2@M:VectorTime],T3,T4,T5]],'LOG@M:Log]]]] .

    eq sresultMonitorObjInsert(T1,T2,T3,T4,T5) =
  '<_:_|_>['M@M:Address,'Monitor.ActorType,'log:_['_`,_['_|->_[T1,'<_;_;_;_;_>['VTS1@M:VectorTime,
        'insert[T2,'GT@M:Float,'VTS2@M:VectorTime],T3,T4,T5]],'LOG@M:Log]]] .


    eq resultMonitorObjInsert(T1,T2,T3,T4,T5,T6) =
	'<_:_|_>['M@M:Address,'Monitor.ActorType,'_`,_['clock:_['_+_['GT@M:Nat,'s_[
		  '0.Zero]]],'log:_['_`,_['_|->_[T1,'<_`,_`,_`,_`,_>['VTS1@M:VectorTime,
		    'insert[T2,'GT@M:Nat,'VTS2@M:VectorTime],T3,T4,'kver[T5,T6]]],'LOG@M:Log]]]] .
  
      eq sresultMonitorObjInsert(T1,T2,T3,T4,T5,T6) =
  '<_:_|_>['M@M:Address,'Monitor.ActorType,'log:_['_`,_['_|->_[T1,'<_;_;_;_;_>['VTS1@M:VectorTime,
        'insert[T2,'GT@M:Float,'VTS2@M:VectorTime],T3,T4,'kver[T5,T6]]],'LOG@M:Log]]] .  

    eq resultMonitorObj(T1,T2) = '<_:_|_>['M@M:Address,'Monitor.ActorType,
            '_`,_['clock:_['_+_['GT@M:Nat,'s_['0.Zero]]],'log:_[
	      'insert[T1, '<_`,_`,_`,_`,_>['_|->_[T2,'GT@M:Nat],'empty.VectorTime,
          'false.Bool,'empty.Set`{KeyVersion`},'empty.Set`{KeyVersion`}],'LOG@M:Log]]]] .



    eq monitorRules(Rl R) = monitorRulesAux(Rl) monitorRules(R)  .
    eq monitorRules(none) = none . 


    eq smonitorRules(MD, Rl R) = smonitorRulesAux(MD, Rl) smonitorRules(MD, R)  .
    eq smonitorRules(MD, none) = none . 

    ceq monitorRulesAux(rl T => T' [ATR] .) = 
	(rl T1 => T2 [ATR] .)
      if '__[T1,T2] := tagLeftRightHand(T,T',false) .

   ceq smonitorRulesAux(MD,rl T => T' [ATR] .) = 
  (rl T1 => T2 [ATR] .)
      if '__[T1,T2] := stagLeftRightHand(MD,T,T',false) .   
    
    
    ceq monitorRulesAux(crl T => T' if CD [ATR] .) =
	  (crl T1 => T2 if CD [ATR] .)
	  if '__[T1,T2] := tagLeftRightHand(T,T',false) .

    ceq smonitorRulesAux(MD, crl T => T' if CD [ATR] .) =
    (crl T1 => T2 if CD [ATR] .)
    if '__[T1,T2] := stagLeftRightHand(MD,T,T',false) .

    op getReplicaID : Term -> Variable .
    
    eq getReplicaID('<_:_|_>[V,'Replica.ActorType,TL]) = V .
    eq getReplicaID('<_:_|`>[V,'Replica.ActorType]) = V .
    eq getReplicaID('<_:_|_>[V,'Client.ActorType,TL]) = V .
    eq getReplicaID('<_:_|`>[V,'Client.ActorType]) = V .
    eq getReplicaID('<_:_|_>[V,'Server.ActorType,TL]) = V .
    eq getReplicaID('<_:_|`>[V,'Server.ActorType]) = V .
    eq getReplicaID(Q[TL]) = getReplicaID(TL) [owise] .
    ceq getReplicaID((T, TL)) = getReplicaID(T), getReplicaID(TL) if TL =/= empty . 
    eq getReplicaID(C) = empty .
    eq getReplicaID(V) = empty .
    
   op tagLeftRightHand : Term Term Bool -> Term .
   op stagLeftRightHand : SModule Term Term Bool -> Term .

    vars Q' : Qid .
    vars T1 T2 T3 T4 T5 T6 : Term .
    vars TL TL1 TL2 TL3 TL' : TermList .

    *** reutrn the attribute value of a given attribute id 
    op getAttr : Qid Term -> TermList .
    eq getAttr(Q,Q'[TL]) = if Q == Q' then TL else  getAttr(Q,TL) fi .

    ceq getAttr(Q,(T,TL)) =
	getAttr(Q,T), getAttr(Q,TL)        
      if TL =/= empty .
    
    eq getAttr(Q,empty) =
	empty .
    var V : Variable .
    var C : Constant .
    
    eq getAttr(Q,V) = empty .
    eq getAttr(Q,C) = empty .

    ops isExecuted isCommitted isAborted isRmtCommitted : Term Term -> Term .

    op getTID : Term -> Term .
    op getTIDfromList : TermList -> TermList . 


    eq getTIDfromList(('<_:_|_>[V,'Txn.ActorType,TL],TL')) = V, getTIDfromList(TL') .
    eq getTIDfromList(('<_:_|`>[V,'Txn.ActorType],TL')) = V, getTIDfromList(TL') .

    eq getTIDfromList(('<_:_|_>[V,'Transaction.ActorType,TL],TL')) = V, getTIDfromList(TL') .
    eq getTIDfromList(('<_:_|`>[V,'Transaction.ActorType],TL')) = V, getTIDfromList(TL') .

    eq getTIDfromList((Q[TL],TL')) = getTIDfromList((TL,TL')) [owise] . 
    eq getTIDfromList((V,TL)) =  if getType(V) == 'Address then V, getTIDfromList(TL) else getTIDfromList(TL) fi .
    eq getTIDfromList((C,TL)) = getTIDfromList(TL) .
    eq getTIDfromList(empty) = empty .

    op diff : TermList TermList -> TermList .
    ceq diff((T1, TL1), TL2) =
      if is T1 subTermOf TL2 then 
        diff(deleteTerm(TL1,T1),deleteTerm(TL2,T1)) 
      else T1, diff(deleteTerm(TL1,T1), TL2) 
      fi
      if TL2 =/= empty .

    eq diff(empty, TL2) = TL2 .
    eq diff(TL1, empty) = TL1 .

    op deleteTerm : TermList Term -> TermList .
    eq deleteTerm(empty, T) = empty .
    eq deleteTerm((T, TL), T) = deleteTerm(TL,T) .
    eq deleteTerm((T, TL), T1) = 
      if T =/= T1 then 
        deleteTerm(TL, T1) 
      else 
        T, deleteTerm(TL, T1)
      fi .



    *** T: LHS, T': RHS 
    *** RHS of executing has a TID which is not in the LHS 
    ceq isExecuted(T, T') = T1
	if TL1 := getAttr('executing:_,T)  /\
     TL2 := getAttr('executing:_,T') /\
     TL3 := getTIDfromList(TL2) /\ 
     TL3 =/= empty /\ 
	   T1 := diff(getTIDfromList(TL1),TL3) .


    ceq isCommitted(T, T') = T1
  if TL1 := getAttr('committed:_,T)  /\
     TL2 := getAttr('committed:_,T') /\
     T1 := diff(getTIDfromList(TL1),getTIDfromList(TL2)) .


    ceq isAborted(T, T') = T1
  if TL1 := getAttr('aborted:_,T)  /\
     TL2 := getAttr('aborted:_,T') /\
     T1 := diff(getTIDfromList(TL1),getTIDfromList(TL2)) .

    ceq isRmtCommitted(T, T') = T1
  if TL1 := getAttr('rmtCommitted:_,T)  /\
     TL2 := getAttr('rmtCommitted:_,T') /\
     T1 := diff(getTIDfromList(TL1),getTIDfromList(TL2)) .


    ceq getTID(T) = T3
	if T2 := getAttr('executing:_,T) /\
	  '<_:_|_>[T3,TL] := T2 .

    ceq getTID(T) = T3
      if T2 := getAttr('executing:_,T) /\
	  '<_:_|`>[T3,TL] := T2 .

    eq getTID(T) = 'nil.Address [owise] .
    

    *** go to fix switch using walter case 
    
    eq isAborted(T,T') = 'nil.Address [owise] .
    eq isCommitted(T,T') = 'nil.Address [owise] .
    eq isExecuted(T,T') = 'nil.Address [owise] .
    eq isRmtCommitted(T,T') = 'nil.Address [owise] .
    
    op getPropagatedTxnsID : Term -> Term .

    ceq getPropagatedTxnsID(T) = T3
    if T1 := getAttr('recPropTxns:_,T)  /\
	  '_;_['propagatedTxns[T3,TL],TL1] := T1 .      

    ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObj,T],'__[resultMonitorObj(T1,getReplicaID(T)),T']]
      if not hasBranch(T') /\ 
        T1 := isExecuted(T,T') /\
        T1 =/= 'nil.Address .

    ceq stagLeftRightHand(MD,T,T',B) =
        '__['__[snewMonitorObj,updateMsgL(MD, T)],'__[sresultMonitorObjInsert(T1,getReplicaID(T)),changeMsgFuncsRHS(MD,T')]]
      if not hasBranch(T') /\ 
        T1 := isExecuted(T,T') /\
        T1 =/= 'nil.Address .   

    *** to fix if_then_else_fi case 

    ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObj,T],
        tagBranch(T,T',resultMonitorObj(T1,getReplicaID(T)), newMonitorObj)]
      if hasBranch(T') /\ 
        T1 := isExecuted(T,T') /\
        T1 =/= 'nil.Address .

    ceq stagLeftRightHand(MD,T,T',B) =
    '__['__[snewMonitorObj,updateMsgL(MD, T)],
    tagBranch(T,changeMsgFuncsRHS(MD,T'),sresultMonitorObjInsert(T1,getReplicaID(T)), snewMonitorObj)]
  if hasBranch(T') /\ 
    T1 := isExecuted(T,T') /\
    T1 =/= 'nil.Address .    

    
  ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObjRecord2(T2),T],
	  '__[resultMonitorObjInsert(T2,
	      getReplicaID(T),'true.Bool,
        getAttr('readSet:_,T1),
        getAttr('writeSet:_,T1),
        getAttr('version:_,T1)),T']]
      if not hasBranch(T') /\ 
        T2 := isCommitted(T,T') /\
        T2 =/= 'nil.Address /\ 
	  T1 := getAttr('executing:_,T) .    


     ceq stagLeftRightHand(MD,T,T',B) =
        '__['__[snewMonitorObjRecord2(T2),updateMsgL(MD, T)],
    '__[sresultMonitorObjInsert(T2,
        getReplicaID(T),'true.Bool,
        getAttr('readSet:_,T1),
        getAttr('writeSet:_,T1),
        getAttr('version:_,T1)),changeMsgFuncsRHS(MD,T')]]
      if not hasBranch(T') /\ 
        T2 := isCommitted(T,T') /\
        T2 =/= 'nil.Address /\ 
    T1 := getAttr('executing:_,T) .  


ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObjRecord2(T2),T],
    '__[resultMonitorObjInsert(T2,
        getReplicaID(T),'true.Bool,
        getAttr('readSet:_,T1),
        getAttr('writeSet:_,T1),
        getAttr('version:_,T1)),T']]
      if not hasBranch(T') /\ 
        T2 := isCommitted(T,T') /\
        T2 =/= 'nil.Address /\ 
    T1 := getAttr('submitted:_,T) .    


ceq stagLeftRightHand(MD,T,T',B) =
        '__['__[snewMonitorObjRecord2(T2),updateMsgL(MD, T)],
    '__[sresultMonitorObjInsert(T2,
        getReplicaID(T),'true.Bool,
        getAttr('readSet:_,T1),
        getAttr('writeSet:_,T1),
        getAttr('version:_,T1)),changeMsgFuncsRHS(MD,T')]]
      if not hasBranch(T') /\ 
        T2 := isCommitted(T,T') /\
        T2 =/= 'nil.Address /\ 
    T1 := getAttr('submitted:_,T) .   


ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObjRecord2(T2),T],
    '__[resultMonitorObjInsert(T2,getReplicaID(T)),T']]
      if not hasBranch(T') /\ 
        T2 := isCommitted(T,T') /\
        T2 =/= 'nil.Address /\ 
        getAttr('submitted:_,T) == empty .  

ceq stagLeftRightHand(MD,T,T',B) =
        '__['__[snewMonitorObjRecord2(T2),updateMsgL(MD, T)],
    '__[sresultMonitorObjInsert(T2,getReplicaID(T)),changeMsgFuncsRHS(MD,T')]]
      if not hasBranch(T') /\ 
        T2 := isCommitted(T,T') /\
        T2 =/= 'nil.Address /\ 
        getAttr('submitted:_,T) == empty .  


   *** to fix if_then_else_fi case  (committed case)
  ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObjRecord2(T2),T],
      tagBranch(T, T',resultMonitorObjInsert(T2,getReplicaID(T),'true.Bool,
                    getAttr('readSet:_,T1),
                    getAttr('writeSet:_,T1),
                    getAttr('version:_,T1)),
              newMonitorObjRecord2(T2))]  
      if hasBranch(T') /\ 
        T2 := isCommitted(T,T') /\
        T2 =/= 'nil.Address /\ 
        T1 := getAttr('executing:_,T) .       

    ceq stagLeftRightHand(MD,T,T',B) =
        '__['__[snewMonitorObjRecord2(T2),updateMsgL(MD, T)],
      tagBranch(T, changeMsgFuncsRHS(MD,T'),sresultMonitorObjInsert(T2,getReplicaID(T),'true.Bool,
                    getAttr('readSet:_,T1),
                    getAttr('writeSet:_,T1),
                    getAttr('version:_,T1)),
              snewMonitorObjRecord2(T2))]  
      if hasBranch(T') /\ 
        T2 := isCommitted(T,T') /\
        T2 =/= 'nil.Address /\ 
        T1 := getAttr('executing:_,T) .        

 
   op  tagBranch : Term Term Term Term -> Term .
   vars TT1 TT2 TT3 : Term .
   
   *** T1: left, T2: brach, T3: result monitor, T4: original monitor 
   eq tagBranch(T1, 'if_then_else_fi[TT1,TT2,TT3], T3, T4) = 
      if isAborted(T1,TT2) =/= 'nil.Address or 
        isCommitted(T1,TT2) =/= 'nil.Address or 
        isExecuted(T1,TT2) =/= 'nil.Address 
        *** or isRmtCommitted(T1,TT2) =/= 'nil.Address 
        then 
           'if_then_else_fi[TT1,'__[T3,TT2],'__[T4,TT3]] 
      else 
           'if_then_else_fi[TT1,'__[T4,TT2],'__[T3,TT3]] 
      fi .
    
   eq tagBranch(T1, '__[TT1,TT2], T3, T4) = 
      if hasBranch(TT1) then '__[tagBranch(T1,TT1,T3,T4),TT2] else '__[TT1,tagBranch(T1,TT2,T3,T4)] fi .


      --- this equation formalizes the abort of a trans 
    --- condition : the executing tran is aborted
     ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObjRecord2(T2),T], 
        '__[resultMonitorObjInsert(T2,
	      getReplicaID(T),'false.Bool,
        getAttr('readSet:_,T1),
        getAttr('writeSet:_,T1),
        getAttr('version:_,T1)),T']]
      if not hasBranch(T') /\ 
          T2 := isAborted(T,T') /\
          T2 =/= 'nil.Address /\ 
          T1 := getAttr('executing:_,T) .

     ceq stagLeftRightHand(MD,T,T',B) =
        '__['__[snewMonitorObjRecord2(T2), updateMsgL(MD, T)],
        '__[sresultMonitorObjInsert(T2,getReplicaID(T),'false.Bool,getAttr('readSet:_,T1),getAttr('writeSet:_,T1),getAttr('version:_,T1)),
            changeMsgFuncsRHS(MD,T')]]
      if not hasBranch(T') /\ 
          T2 := isAborted(T,T') /\
          T2 =/= 'nil.Address /\ 
          T1 := getAttr('executing:_,T) .   

      ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObjRecord2(T2),T], 
        '__[resultMonitorObjInsert(T2,getReplicaID(T)),T']]
      if not hasBranch(T') /\ 
          T2 := isAborted(T,T') /\
          T2 =/= 'nil.Address /\ 
          getAttr('executing:_,T) == empty .       

      ceq stagLeftRightHand(MD,T,T',B) =
        '__['__[newMonitorObjRecord2(T2),updateMsgL(MD, T)], 
        '__[sresultMonitorObjInsert(T2,getReplicaID(T)),changeMsgFuncsRHS(MD,T')]]
      if not hasBranch(T') /\ 
          T2 := isAborted(T,T') /\
          T2 =/= 'nil.Address /\ 
          getAttr('executing:_,T) == empty .         


     ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObjRecord2(T2),T], 
        '__[resultMonitorObjInsert(T2,
        getReplicaID(T),'false.Bool,
        getAttr('readSet:_,T1),
        getAttr('writeSet:_,T1),
        getAttr('version:_,T1)),T']]
      if not hasBranch(T') /\ 
          T2 := isAborted(T,T') /\
          T2 =/= 'nil.Address /\ 
          T1 := getAttr('submitted:_,T)  .  

       ceq stagLeftRightHand(MD,T,T',B) =
        '__['__[snewMonitorObjRecord2(T2),updateMsgL(MD, T)], 
        '__[sresultMonitorObjInsert(T2,
        getReplicaID(T),'false.Bool,
        getAttr('readSet:_,T1),
        getAttr('writeSet:_,T1),
        getAttr('version:_,T1)),changeMsgFuncsRHS(MD,T')]]
      if not hasBranch(T') /\ 
          T2 := isAborted(T,T') /\
          T2 =/= 'nil.Address /\ 
          T1 := getAttr('submitted:_,T)  .  


    --- condition : the executing tran is aborted
     ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObjRecord2(T2),T],
        tagBranch(T,T', 
            resultMonitorObjInsert(T2,getReplicaID(T),'false.Bool,
                getAttr('readSet:_,T1),
                getAttr('writeSet:_,T1),
                getAttr('version:_,T1)),
            newMonitorObjRecord2(T2))]
      if hasBranch(T') /\ 
         T2 := isAborted(T,T') /\
         T2 =/= 'nil.Address /\ 
         T1 := getAttr('executing:_,T)  .      


     ceq stagLeftRightHand(MD,T,T',B) =
        '__['__[snewMonitorObjRecord2(T2),updateMsgL(MD, T)],
        tagBranch(T,changeMsgFuncsRHS(MD,T'), 
            sresultMonitorObjInsert(T2,getReplicaID(T),'false.Bool,
                getAttr('readSet:_,T1),
                getAttr('writeSet:_,T1),
                getAttr('version:_,T1)),
            snewMonitorObjRecord2(T2))]
      if hasBranch(T') /\ 
         T2 := isAborted(T,T') /\
         T2 =/= 'nil.Address /\ 
         T1 := getAttr('executing:_,T)  .   


        ceq tagLeftRightHand(T,T',B) =
        '__['__[newMonitorObjRecord2(T2),T],
    '__[resultMonitorObjInsert(T2,getReplicaID(T)),T']]
      if not hasBranch(T') /\ 
        T2 := isRmtCommitted(T,T') /\
        T2 =/= 'nil.Address .     

        ceq stagLeftRightHand(MD,T,T',B) =
        '__['__[snewMonitorObjRecord2(T2),updateMsgL(MD, T)],
    '__[sresultMonitorObjInsert(T2,getReplicaID(T)),changeMsgFuncsRHS(MD,T')]]
      if not hasBranch(T') /\ 
        T2 := isRmtCommitted(T,T') /\
        T2 =/= 'nil.Address .   

    op tagRightHand : Term TermList -> TermList .
    op hasBranch : TermList -> Bool .
    eq hasBranch('__[TL]) = hasBranch(TL) .
    eq hasBranch('if_then_else_fi[T1,T2,T3]) = true .
    ceq hasBranch((T, TL)) = hasBranch(T) or hasBranch(TL) if TL =/= empty .
    eq hasBranch(empty) = false .
    eq hasBranch(T) = false [owise] .


    --- to monitor each result in the if_then_else_fi statement 
    ceq tagRightHand(T, (T', TL)) =
	(tagRightHand(T, T'), tagRightHand(T,TL))
      if TL =/= empty .
    eq tagRightHand(T, empty) = empty .
    
    eq tagRightHand(T,'__[TL]) = '__[tagRightHand(T, TL)] . 

    eq tagRightHand(T, 'if_then_else_fi[T1,T2,T3]) =
	'if_then_else_fi[T1,tagRightHand(T,T2),tagRightHand(T,T3)]  .

    ceq tagRightHand(T, '<_:_|_>[T1,'Replica.Replica,TL]) =
      T3 
      if '__[T2,T3] := tagLeftRightHand(T,'<_:_|_>[T1,'Replica.Replica,TL],true) .
    
    eq tagRightHand(T, T') = T' [owise] .

      --- nothing changed, no tag

    eq tagLeftRightHand(T,T',B) =
        '__[T,T'] [owise] .


    eq stagLeftRightHand(MD,T,T',B) =
        '__[updateMsgL(MD, T),changeMsgFuncsRHS(MD,T')] [owise] .
    

    ---eq tagID(Q) = string2qidList(string(Q) + "-TAGGED") .
    eq tagID(Q) = string2qidList(string(Q)) .
    
endfm
    
    
mod TAG-DATABASE-HANDLING is
    protecting DATABASE-HANDLING .
    protecting META-TAG-SIGNATURE .    
    including TAG-BUILTINS .
    
    var N 				: Nat .
    var X@Database         : DatabaseClass .
    var O 				: Oid .
    var QI 				: Qid .
    vars QIL QIL' QIL''	        : QidList .
    vars T T' T''		        : Term .
    vars H H'			        : Header .
    var Q 				: Qid .
    vars DB DB' DB''           	: Database .
    var F					: Qid .
    vars MD M	M'	M''		: Module .
    var Atts 			        : AttributeSet .
    vars RP RP'			: ResultPair? .
    vars ME ME' ME''	                : ModuleExpression . 
    
    crl [handle-tag-func] : 
        < O         : X@Database | db : DB,
				   input     : ('monitor_.[T]),
				   output  : nil, Atts > => 
        < O         : X@Database | db : DB'',
				   input     : nilTermList,
				    output : ('New 'module '\g Q '\o 'with 'monitors 'generated!),
				   Atts  >
      if  < DB' ; ME' > := evalModExp(parseModExp(T), DB) /\
	  M := getTopModule(ME', DB') /\ 
	  M' := tag(M) /\
	  Q := getName(M') /\ 
	  DB'' := insTermModule(Q,M',DB') .

    crl [handle-smonitor-func] : 
        < O         : X@Database | db : DB,
           input     : ('smonitor_.[T]),
           output  : nil, Atts > => 
        < O         : X@Database | db : DB'',
           input     : nilTermList,
            output : ('New 'module '\g Q '\o 'with 'monitors 'generated!),
           Atts  >
      if  < DB' ; ME' > := evalModExp(parseModExp(T), DB) /\
    M := getTopModule(ME', DB') /\ 
    M' := tagS(getFlatModule(ME',DB'),M) /\
    Q := getName(M') /\ 
    DB'' := insTermModule(Q,M',DB') .

endm 


mod MONITORING is
    extending LOOP-MODE .
    protecting TAG-DATABASE-HANDLING .
    
    subsort Object < State .
    op o : -> Oid .
    
    var H 				: Header .
    var O 				: Oid .
    var X@Database 		        : DatabaseClass .
    var QI 				: Qid .
    vars QIL QIL' QIL''             : QidList .
    var TL 				: TermList .
    var DB 				: Database .
    var Atts 			: AttributeSet .
    
    op init 			: -> System .
    
    
    
    rl [init] : init => [nil,
	  < o		: Database | 
	  db		: initialDatabase, 
	  input 	: nilTermList , 
          default      : 'CONVERSION ,
	  output 	: nil >,
	  ('\s '\s '\s '\s '\s '\g string2qidList("Welcome to CAT!") '\o)] .
    
    rl [input] : 	[QI QIL, 
	  < O 			: X@Database | 
	  input 		: nilTermList, 
	  output 		: nil, Atts >,  
	  QIL' ] => 
      if metaParse(TagGRAMMAR, QI QIL, '@Input@) :: ResultPair
	then
	  [nil, 
	    < O 		: X@Database | 
	    input 		: getTerm(metaParse(TagGRAMMAR, QI QIL, '@Input@)), 
	    output 		: nil, Atts >, 
	    QIL']
	else 
	  [nil, 
	    < O 		: X@Database | 
	    input 		: nilTermList, 
	    output 		: ('\r 'WARNING: '\o printSyntaxError(metaParse(TagGRAMMAR, QI QIL, '@Input@), QI QIL) '\n 
	      'ERROR: 'No 'parse 'for 'input.), Atts >, 
	    QIL' ]
	fi .
    
    rl 	[output] : 	[QIL, < O : X@Database | 
	  output 	: (QI QIL'), Atts >,
	  QIL''] =>
	[QIL, < O 	: X@Database | 
	  output 	: nil, Atts >, (QI QIL' QIL'')] .
endm

set print conceal on .

*** print conceal mod_is_sorts_._____endm .
*** print conceal fmod_is_sorts_.____endfm .
print conceal db .
    
loop init .
    
